---
title: Android库发布到MavenCentral
date: 2021-09-25 13:57:17
tags:
--- 

> 与jCenter是由jForg运营，在bintray.com做管理一样，Maven Central是由sonatype运营的，那么首先需要去注册一个sonatype的账号并获得仓库使用许可。

1. 注册。[https://issues.sonatype.org](https://issues.sonatype.org)
2. 提交发布申请。[https://issues.sonatype.org/secure/CreateIssue.jspa?issuetype=21&amp;pid=10134](https://issues.sonatype.org/secure/CreateIssue.jspa?issuetype=21&amp;pid=10134)
	- 项目类型是 Community Support - Open Source Project Repository Hosting
	- groupId 对应的域名你需要添加TXT记录，github账号需要建立对应OSSRH-73706的空仓库
3. 使用 GPG 生成密钥对。下载地址：[https://www.gnupg.org/download/](https://www.gnupg.org/download/)。用得到的指令有如下几条：
	- `gpg --version` 检查安装成功没
	- `gpg --gen-key` 生成密钥对
	- `gpg --list-keys` 查看公钥
	- `gpg --keyserver hkp://pool.sks-keyservers.net --send-keys 公钥ID` 将公钥发布到 PGP 密钥服务器
	- `gpg --keyserver hkp://pool.sks-keyservers.net --recv-keys 公钥ID` 查询公钥是否发布成功
	- `gpg --export-secret-keys -o xx/xx.gpg` The secring.gpg file has been removed in GPG 2.1
		```
		FAILURE: Build failed with an exception.
		
		* What went wrong:
		Could not evaluate onlyIf predicate for task ':library:signArchives'.
		> Unable to read secret key from file: /xx/xx.gpg (it may not be a PGP secret key ring)
		```

4. 项目根目录配置publish-mavencentral.gradle
```
apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["PUBLISH_GROUP_ID"] = ''	// GROUP_ID
ext["PUBLISH_ARTIFACT_ID"] = ''	// ARTIFACT_ID
ext["PUBLISH_VERSION"] = ''	// VERSION

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}
publishing {
    publications {
        release(MavenPublication) {
            // The coordinates of the library, being set from variables that
            // we'll set up in a moment


            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            // Two artifacts, the `aar` and the sources
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            artifact androidSourcesJar

            // Self-explanatory metadata for the most part
            pom {
                name = PUBLISH_ARTIFACT_ID
                description = '你的项目描述'
                // If your project has a dedicated site, use its URL here
                url = 'https://github.com/hdinson/BlingBling.git'
                licenses {
                    license {
                        //协议类型，一般默认Apache License2.0的话不用改：
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = '用户ID'
                        name = '用户名'
                        email = '邮箱'
                    }
                }
                // Version control info, if you're using GitHub, follow the format as seen here
                scm {
                     //修改成你的Git地址：
                    connection = 'scm:git:github.com/xxx/xxxx.git'
                    developerConnection = 'scm:git:ssh://github.com/xxx/xxxx.git'
                    //分支地址：
                    url = 'https://github.com/xxx/xxxx/tree/master'
                }
                // A slightly hacky fix so that your POM will include any transitive dependencies
                // that your library builds upon
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        // The repository to publish to, Sonatype/MavenCentral
        maven {
            // This is an arbitrary name, you may also use "mavencentral" or
            // any other name that's descriptive for you
            name = "项目名称"

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            // You only need this if you want to publish snapshots, otherwise just set the URL
            // to the release repo directly
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            // The username and password we've fetched earlier
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}
signing {
    sign publishing.publications
}
```

5. 根目录下local.properties文件，添加如下：
```
signing.keyId=XXXXXXXX
signing.password=密码
signing.secretKeyRingFile=你的GPG签名秘钥文件.gpg
ossrhUsername=sonatype帐号
ossrhPassword=sonatype密码
```

6. 项目build文件，添加
```
apply from: "publish-mavencentral.gradle"
```

7. 上传
	- Sync后，先通过Gradle编译release版本，双击Module名称/Tasks/build/assemble：
	- 完成后，上传到Maven Central，双击Module名称/Tasks/publishing/publicReleasePublicationToXXXRepository

8. 在 OSS 中发布构件。进入  [https://s01.oss.sonatype.org/](https://s01.oss.sonatype.org/)，点击"Staging Repositories" -> 在搜索栏输入你的 groupId -> 勾选你的构件并点击 close -> 点击 tab 栏的 release。


> 以后的发布流程：

> 1. 构件准备好之后，在命令行上传构建；
> 2. 在[https://s01.oss.sonatype.org/](https://s01.oss.sonatype.org/) “close”并“release”构件；
> 3. 等待同步好（大约2小时多）之后，就可以使用了